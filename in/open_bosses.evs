#memory(
    string_key(0x0546)..string_key(0x232b), // last half of string keys
    function_key(0x0000)..function_key(0x232b), // TODO: all function keys?

    0x300000..0x3fffff, // extension

    // reserved: <0x23b9>
    <0x2272>..<0x2558>,

    <0x2834>..<0x28ff>
)
#include("in/core.evs")

#patch(
    // "void_maps",

    "skip_intro",
    // "brian",
    // "camera_hack",
    // "free_graveyard_ids",
    // "save_file_growth",

    // asm
    // "debug_menu", // uses 0x3f0000…???
    "_hook_input", // 0x3f0000…0x310000
        "hotkeys", // uses 0x3f00000…???
        // "room_timer", // uses 0x300000…???
    // "_hook_trigger",
)

// hotkeys

@install()
@inject(ADDRESS.HOTKEY_START)
fun hotkey_start() {
    debug_subtext("Start");
}
@install()
@inject(ADDRESS.HOTKEY_START_L)
fun hotkey_start_l() {
    debug_subtext("Start+L");
}
@install()
@inject(ADDRESS.HOTKEY_START_R)
fun hotkey_start_r() {
    debug_subtext("Start+R");

    attribute(BOY, NO_CLIP, True);
    attribute(BOY, INVINCIBLE_TEMP, True);
    debug_boy();
}

// maps

@install(ADDRESS.INTRO_FIRST_CODE_EXECUTED)
fun intro_skip() {
    // map_transition(flowers, start, NONE);
    
    map_transition(rimsala, south, NONE);
}

group boss_rimsala() {
    enum BOSS_RIMSALA {
        DEBUG = False,

        TYPE_RIMSALA_HEAD = ENEMY.RIMSALA_BOSS_2,
        TYPE_RIMSALA = ENEMY.RIMSALA_BOSS_1,
        TYPE_STATUE = ENEMY.RIMSALA_STATUTE_ENTITY,

        ID_RIMSALA_HEAD = arg[0x10],
        ID_RIMSALA = arg[0x12],

        ID_STATUE_L1 = arg[0x14],
        ID_STATUE_L2 = arg[0x16],
        ID_STATUE_L3 = arg[0x18],
        ID_STATUE_R1 = arg[0x1a],
        ID_STATUE_R2 = arg[0x1c],
        ID_STATUE_R3 = arg[0x1e],

        RIMSALA_Z_OFFSET = 0d80, // [pixel]
        RIMSALA_Z_DISTANCE = 0d95, // [pixel], has to be on the screen

        TIME_RELEASE_PHASE = 0d600, // [frames]
        TIME_CATCH_PHASE = 0d300, // [frames]

        STATUE_TRIGGER_SPELL = CAST.FLASH,
        STATUE_TRIGGER_POWER = 0d90, // [damage]
        STATUE_TRIGGER_INTERVAL = 0d420, // [frames] (7s)
        STATUE_HEAL_INTERVAL = 0d1080, // [frames] (18s), the timer only counts down while the statue is down
        WITH_STATUE_ANIMATION = False,
        WITH_STATUE_OBJECTS = True,
    }
    @install()
    fun statue_triggered() {
        heal(SCRIPT_OWNER, 0d0, False);

        <SCRIPT_OWNER>[GENERAL_PURPOSE] = (<SCRIPT_OWNER>[GENERAL_PURPOSE] & 0xff00) + 0d1;

        attribute(SCRIPT_OWNER, INVINCIBLE, True);
        if(BOSS_RIMSALA.WITH_STATUE_OBJECTS) {
            object[<SCRIPT_OWNER>[GENERAL_PURPOSE] >> 0d8] = 0d1;
        }

        if(BOSS_RIMSALA.WITH_STATUE_ANIMATION) {
            animate(SCRIPT_OWNER, LOOP, DEFAULT);
        }

        if(BOSS_RIMSALA.DEBUG) {
            debug_subtext("statue dead");
            debug_memory(<SCRIPT_OWNER>[GENERAL_PURPOSE], 0d0);
        }
    }
    fun statue_trigger(statue, target:CHARACTER) {
        if((statue[GENERAL_PURPOSE] & 0xff) == 0d0) {
            cast(statue, target, BOSS_RIMSALA.STATUE_TRIGGER_SPELL, BOSS_RIMSALA.STATUE_TRIGGER_POWER);

            if(BOSS_RIMSALA.WITH_STATUE_ANIMATION) {
                animate(statue, ONCE, SLASH_1);
            }
        }
    }
    @install()
    @async()
    fun statue_tracker(rimsala, statue_l1, statue_l2, statue_l3, statue_r1, statue_r2, statue_r3) {
        if(BOSS_RIMSALA.DEBUG) {
            debug_subtext("+statue_tracker");
        }

        sleep(BOSS_RIMSALA.STATUE_TRIGGER_INTERVAL);

        while!(dead(rimsala)) {
            if(BOSS_RIMSALA.DEBUG) {
                debug_subtext("statue_trigger");
            }

            arg[0x10] = randrange(0d6);
            if(arg[0x10] == 0d0) {
                statue_trigger(statue_l1, BOY);
            } else if(arg[0x10] == 0d1) {
                statue_trigger(statue_l2, DOG);
            } else if(arg[0x10] == 0d2) {
                statue_trigger(statue_l3, BOY);
            } else if(arg[0x10] == 0d3) {
                statue_trigger(statue_r1, DOG);
            } else if(arg[0x10] == 0d4) {
                statue_trigger(statue_r2, BOY);
            } else if(arg[0x10] == 0d5) {
                statue_trigger(statue_r3, DOG);
            }

            sleep(BOSS_RIMSALA.STATUE_TRIGGER_INTERVAL);
        }

        if(BOSS_RIMSALA.DEBUG) {
            debug_subtext("-statue_tracker");
        }
    }
    fun heal_statue(statue) {
        if((statue[GENERAL_PURPOSE] & 0xff) > 0d0) {
            heal(statue, 0d999, True);
            if(BOSS_RIMSALA.WITH_STATUE_ANIMATION) {
                animate(statue, ONCE, DEFAULT);
            }
            if(BOSS_RIMSALA.WITH_STATUE_OBJECTS) {
                object[statue[GENERAL_PURPOSE] >> 0d8] = 0d0;
            }

            attribute(statue, INVINCIBLE, False);
            statue[GENERAL_PURPOSE] = (statue[GENERAL_PURPOSE] & 0xff00) + 0d0;
        }
    }
    fun heal_statues() {
        heal_statue(BOSS_RIMSALA.ID_STATUE_L1);
        heal_statue(BOSS_RIMSALA.ID_STATUE_L2); 
        heal_statue(BOSS_RIMSALA.ID_STATUE_L3); 
        heal_statue(BOSS_RIMSALA.ID_STATUE_R1); 
        heal_statue(BOSS_RIMSALA.ID_STATUE_R2); 
        heal_statue(BOSS_RIMSALA.ID_STATUE_R3); 
    }
    fun up() {
        if(BOSS_RIMSALA.DEBUG) {
            debug_subtext("up");
        }

        BOSS_RIMSALA.ID_RIMSALA_HEAD[GENERAL_PURPOSE] += BOSS_RIMSALA.TIME_RELEASE_PHASE;
        if(BOSS_RIMSALA.ID_RIMSALA_HEAD[GENERAL_PURPOSE] > BOSS_RIMSALA.STATUE_HEAL_INTERVAL) {
            if(BOSS_RIMSALA.DEBUG) {
                debug_subtext("heal_statue");
            }

            heal_statues();

            BOSS_RIMSALA.ID_RIMSALA_HEAD[GENERAL_PURPOSE] -= BOSS_RIMSALA.STATUE_HEAL_INTERVAL;
        }

        while(BOSS_RIMSALA.ID_RIMSALA_HEAD[Y] > (arg[0x02] - BOSS_RIMSALA.RIMSALA_Z_OFFSET - BOSS_RIMSALA.RIMSALA_Z_DISTANCE)) {
            BOSS_RIMSALA.ID_RIMSALA_HEAD[Y] -= 0d1;
            if(BOSS_RIMSALA.ID_RIMSALA[GENERAL_PURPOSE] == 0d0) {
                BOSS_RIMSALA.ID_RIMSALA[Y] = BOSS_RIMSALA.ID_RIMSALA_HEAD[Y] + BOSS_RIMSALA.RIMSALA_Z_OFFSET;
            }

            yield();
        }
    }
    fun down() {
        if(BOSS_RIMSALA.DEBUG) {
            debug_subtext("down");
        }

        while(BOSS_RIMSALA.ID_RIMSALA_HEAD[Y] < (arg[0x02] - BOSS_RIMSALA.RIMSALA_Z_OFFSET)) {
            BOSS_RIMSALA.ID_RIMSALA_HEAD[Y] += 0d1;
            if(BOSS_RIMSALA.ID_RIMSALA[GENERAL_PURPOSE] == 0d0) {
                BOSS_RIMSALA.ID_RIMSALA[Y] = BOSS_RIMSALA.ID_RIMSALA_HEAD[Y] + BOSS_RIMSALA.RIMSALA_Z_OFFSET;
            }

            yield();
        }
    }
    fun intro() {
        if(BOSS_RIMSALA.DEBUG) {
            debug_subtext("intro");
        }

        BOSS_RIMSALA.ID_RIMSALA_HEAD[Y] = arg[0x02] - BOSS_RIMSALA.RIMSALA_Z_OFFSET - BOSS_RIMSALA.RIMSALA_Z_DISTANCE;
        BOSS_RIMSALA.ID_RIMSALA[Y] = BOSS_RIMSALA.ID_RIMSALA_HEAD[Y] + BOSS_RIMSALA.RIMSALA_Z_OFFSET;

        cast(BOSS_RIMSALA.ID_RIMSALA, BOTH, CONFOUND, 0d24);

        sleep(0d60);

        down();

        statue_tracker(BOSS_RIMSALA.ID_RIMSALA, BOSS_RIMSALA.ID_STATUE_L1, BOSS_RIMSALA.ID_STATUE_L2, BOSS_RIMSALA.ID_STATUE_L3, BOSS_RIMSALA.ID_STATUE_R1, BOSS_RIMSALA.ID_STATUE_R2, BOSS_RIMSALA.ID_STATUE_R3);
    }
    fun release_rimsala() {
        BOSS_RIMSALA.ID_RIMSALA[GENERAL_PURPOSE] = 0d1;

        animate(BOSS_RIMSALA.ID_RIMSALA, ONCE, DEFAULT);
        control(BOSS_RIMSALA.ID_RIMSALA, False);

        attribute(BOSS_RIMSALA.ID_RIMSALA, INVINCIBLE_TEMP, True);
        attribute(BOSS_RIMSALA.ID_RIMSALA, ROOT, False);

        up();
    }
    fun catch_rimsala() {
        down();
        walk(BOSS_RIMSALA.ID_RIMSALA, TILE_ABSOLUTE, arg[0x00] >> 0d3, arg[0x02] >> 0d3, BOSS_RIMSALA.ID_RIMSALA);

        face(BOSS_RIMSALA.ID_RIMSALA, SOUTH);
        animate(BOSS_RIMSALA.ID_RIMSALA, LOOP, ANIMATION_ALL.DEFAULT);

        attribute(BOSS_RIMSALA.ID_RIMSALA, ROOT, True);
        attribute(BOSS_RIMSALA.ID_RIMSALA, INVINCIBLE_TEMP, False);
        BOSS_RIMSALA.ID_RIMSALA[GENERAL_PURPOSE] = 0d0;
    }
    fun add_statue(id:BOSS_RIMSALA, x, y, object_id) {
        add_enemy(BOSS_RIMSALA.TYPE_STATUE, x, y);
        id = <LAST_ENTITY>;
        attach_script(LAST_ENTITY, DAMAGE, reference(statue_triggered));
        id[GENERAL_PURPOSE] = object_id << 0d0008;

        control(LAST_ENTITY, True);
    }
    @install()
    @async()
    fun add_rimsala(x, y) {
        x <<= 0d3;
        y <<= 0d3;

        _add_enemy(BOSS_RIMSALA.TYPE_RIMSALA_HEAD, x, y);
        BOSS_RIMSALA.ID_RIMSALA_HEAD = <LAST_ENTITY>;
        attribute(LAST_ENTITY, INVINCIBLE_TEMP, True);

        _add_enemy(BOSS_RIMSALA.TYPE_RIMSALA, x, y);
        BOSS_RIMSALA.ID_RIMSALA = <LAST_ENTITY>;
        animate(LAST_ENTITY, LOOP, ANIMATION_ALL.DEFAULT);
        attribute(LAST_ENTITY, INVINCIBLE_TEMP, True);

        add_statue(ID_STATUE_L1, 0d09, 0d32, 0d05); // l1
        add_statue(ID_STATUE_L2, 0d17, 0d24, 0d04); // l2
        add_statue(ID_STATUE_L3, 0d25, 0d16, 0d00); // l3
        add_statue(ID_STATUE_R1, 0d43, 0d16, 0d01); // r1
        add_statue(ID_STATUE_R2, 0d51, 0d24, 0d02); // r2
        add_statue(ID_STATUE_R3, 0d59, 0d32, 0d03); // r3

        intro();

        while!(dead(BOSS_RIMSALA.ID_RIMSALA)) {
            release_rimsala();
            sleep(BOSS_RIMSALA.TIME_RELEASE_PHASE);

            catch_rimsala();
            sleep(BOSS_RIMSALA.TIME_CATCH_PHASE);
        }
    }
};

map rimsala(RIMSALA) {
    enum entrance {
        south = entrance(0x21, 0x3e, NORTH)
    }

    enum stepon_trigger {
        exit_south = @install() {
            debug_subtext("S=0");
        },
        stepon_1 = @install() {
            debug_subtext("S=1");
        },
        spawn_boss_2 = @install() {
            debug_subtext("S=2");

            if(<0x2834> == 0d0) {
                <0x2834> = 0d1;
                add_rimsala(0d34, 0d27);
            }
        },
        stepon_3 = @install() {
            debug_subtext("S=3");
        },
        spawn_boss_1 = @install() {
            debug_subtext("S=4");
        }
    }

    fun trigger_enter() {
        fade_in();
    }
};
